buildscript {
	ext {
		kotlinVersion = '1.3.0'
		springBootVersion = '2.1.0.RELEASE'
		dockerUserId = "artun"
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
	}
}

plugins {
	id 'com.palantir.docker' version '0.20.1'
	id "org.sonarqube" version "2.6"
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'
apply plugin: 'jacoco'

group = 'com.gamezeug'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}
compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "1.8"
	}
}
bootJar {
	mainClassName = 'com.gamezeug.authserver.AuthServerApplicationKt'
}

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/milestone" }
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('org.springframework.security.oauth:spring-security-oauth2:2.3.4.RELEASE')
	implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
	implementation('org.jetbrains.kotlin:kotlin-reflect')
	implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
	implementation('org.glassfish.jaxb:jaxb-runtime:2.3.0')
	
	testImplementation('org.springframework.boot:spring-boot-starter-test')
}

jacoco {
    toolVersion = "0.8.2"
}
jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

task unpack(type: Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/dependency")
}
docker {
	name "${dockerUserId}/${bootJar.baseName}"
	copySpec.from(tasks.unpack.outputs).into("dependency")
	buildArgs(['DEPENDENCY': "dependency"])
}
